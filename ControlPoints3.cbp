// Control Point Options
DEFINE controlPointRange 4
DEFINE maxControlPointIntegrity 200
DEFINE scoreTarget 1680000

//// Control Points v3.0 ////
FUNC createControlPoint
	summon ArmorStand |1| |2| |3| {Tags:["BiolabControlPoint","BCP|0|"],NoGravity:1b,Marker:1b,Invisible:1b,Invulnerable:1b,PerisistenceRequired:1b,Small:1b}
	USE @e[BCP|0|]
		summon Shulker ~ ~ ~ {CustomName:"Control Point |0|",CustomNameVisible:1b,Tags:["BiolabControlPointVisual","BCP|0|V"],Invulnerable:1b,Glowing:1b,NoAI:1b,PerisistenceRequired:1b}
	UNUSE
	scoreboard players set @e[BCP|0|] BiolabCPI DIV(maxControlPointIntegrity;2)
	scoreboard players set @e[BCP|0|] BiolabCPIS 1
	@e[BCP|0|] setblock ~ ~1 ~ minecraft:stained_glass 0
	scoreboard teams join BiolabCPNeutral @e[BCP|0|]
	scoreboard teams join BiolabCPNeutral @e[BCP|0|V]
END

FUNC enableControlPoints
	scoreboard objectives add BiolabCPI dummy CP Integrity
	scoreboard objectives add BiolabCPIS dummy CP Integrity Status
	scoreboard objectives add BiolabBCP dummy Blink CP
	scoreboard objectives add BiolabCPPC dummy CP Player Count
	scoreboard objectives add BiolabTS dummy Team Scores
	scoreboard players set 20 BiolabTemp 20
	
	createControlPoint(A;-42;41;87)
	createControlPoint(B;-20;47;84)
	
	scoreboard teams join BiolabCPBlue Blue_Team
	scoreboard teams join BiolabCPRed Red_Team
	scoreboard players set Blue_Team BiolabTS 0
	scoreboard players set Red_Team BiolabTS 0
	scoreboard objectives setdisplay sidebar BiolabTS
	activate(controlPointUpdateClock)
END

:controlPointUpdateClock CLOCK 16 0 1
// Reset blink
scoreboard players set @e[BiolabControlPoint] BiolabBCP 0

// Reset player presence
scoreboard players set @e[BiolabControlPoint] BiolabCPPC 0

// Detect player presence
@e[BiolabControlPoint] @p[r=controlPointRange,team=BiolabBlue,m=2,BiolabMAX=0] scoreboard players add @e[BiolabControlPoint,c=1] BiolabCPPC 1
@e[BiolabControlPoint] @p[r=controlPointRange,team=BiolabRed,m=2,BiolabMAX=0] scoreboard players add @e[BiolabControlPoint,c=1] BiolabCPPC 2

// Increasing integrity
@e[team=BiolabCPBlue,BiolabCPPC=1,BiolabCPI<maxControlPointIntegrity] BiolabCPI + 1
@e[team=BiolabCPRed,BiolabCPPC=2,BiolabCPI<maxControlPointIntegrity] BiolabCPI + 1

// Decreasing integrity
@e[BiolabCPPC=1,team=!BiolabCPBlue,BiolabCPI>0] BiolabCPI - 1
@e[BiolabCPPC=2,team=!BiolabCPRed,BiolabCPI>0] BiolabCPI - 1

// Blink tags
scoreboard players set @e[BiolabCPPC=1,team=!BiolabCPBlue] BiolabBCP 1
scoreboard players set @e[BiolabCPPC=2,team=!BiolabCPRed] BiolabBCP 1

// Ownership checks
scoreboard players tag @e[BiolabCPI=0,team=BiolabCPNeutral,BiolabCPPC=1] add CPtoBlue
scoreboard players tag @e[BiolabCPI=0,team=BiolabCPNeutral,BiolabCPPC=2] add CPtoRed
scoreboard players tag @e[BiolabCPI=0,team=!BiolabCPNeutral] add CPtoNeutral

@e[CPtoBlue] playsound minecraft:entity.experience_orb.touch voice @a[team=BiolabBlue] ~ ~ ~
@e[CPtoBlue] tellraw @a[BiolabPlayer] ["",{"text":"[","color":"yellow"},{"text":"Biolab","color":"green"},{"text":"]","color":"yellow"},": ",{"text":"Blue","color":"blue"}," team has captured ",{"selector":"@e[tag=BiolabControlPointVisual,c=1]"},"!"]
@e[CPtoBlue] BiolabCPI = 1
scoreboard teams join BiolabCPBlue @e[CPtoBlue]

@e[CPtoRed] playsound minecraft:entity.experience_orb.touch voice @a[team=BiolabRed] ~ ~ ~
@e[CPtoRed] tellraw @a[BiolabPlayer] ["",{"text":"[","color":"yellow"},{"text":"Biolab","color":"green"},{"text":"]","color":"yellow"},": ",{"text":"Red","color":"red"}," team has captured ",{"selector":"@e[tag=BiolabControlPointVisual,c=1]"},"!"]
@e[CPtoRed] BiolabCPI = 1
scoreboard teams join BiolabCPRed @e[CPtoRed]

@e[CPtoNeutral] playsound minecraft:entity.experience_orb.touch voice @a ~ ~ ~
@e[CPtoNeutral] tellraw @a[BiolabPlayer] ["",{"text":"[","color":"yellow"},{"text":"Biolab","color":"green"},{"text":"]","color":"yellow"},": ",{"selector":"@e[tag=BiolabControlPointVisual,c=1]"}," has been lost!"]
@e[CPtoNeutral] BiolabCPI = DIV(maxControlPointIntegrity;2)
scoreboard teams join BiolabCPNeutral @e[CPtoNeutral]

scoreboard players tag @e[CPtoBlue] remove CPtoBlue
scoreboard players tag @e[CPtoRed] remove CPtoRed
scoreboard players tag @e[CPtoNeutral] remove CPtoNeutral

// CPIS is 2 when more than 50%, 1 when less than or equal to 50%
scoreboard players set @e[BiolabCPI>DIV(maxControlPointIntegrity;2)] BiolabCPIS 2
scoreboard players set @e[BiolabCPI<=DIV(maxControlPointIntegrity;2)] BiolabCPIS 1

// Blinking
@e[BiolabBCP=1] scoreboard players operation @e[BiolabBCP=1,c=1] BiolabTemp = @e[BiolabBCP=1,c=1] BiolabCPI
scoreboard players operation @e[BiolabBCP=1] BiolabTemp %= 20 BiolabTemp
@e[BiolabBCP=1,BiolabTemp=10] playsound minecraft:block.note.pling voice @a ~ ~ ~
USE @e[BiolabBCP=1,BiolabTemp>10] 
	setblock ~ ~1 ~ minecraft:stained_glass 0
	scoreboard teams join BiolabCPNeutral @e[BiolabControlPointVisual,r=1,c=1,team=!BiolabCPNeutral]
UNUSE
scoreboard players set @e[BiolabBCP=1,BiolabTemp<=10] BiolabBCP 0

// Reset visuals
@e[BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=2] setblock ~ ~1 ~ minecraft:stained_glass 11
@e[BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=1] setblock ~ ~1 ~ minecraft:stained_glass 3
@e[BiolabBCP=0,team=BiolabCPBlue] scoreboard teams join BiolabCPBlue @e[BiolabControlPointVisual,r=1,c=1,team=!BiolabCPBlue]

@e[BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=2] setblock ~ ~1 ~ minecraft:stained_glass 14
@e[BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=1] setblock ~ ~1 ~ minecraft:stained_glass 6
@e[BiolabBCP=0,team=BiolabCPRed] scoreboard teams join BiolabCPRed @e[BiolabControlPointVisual,r=1,c=1,team=!BiolabCPRed]

// Inventory indicators
@e[BCPA,BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 11 {display:{Name:"Control Point A"}}
@e[BCPA,BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 14 {display:{Name:"Control Point A"}}
@e[BCPA,BiolabBCP=0,team=BiolabCPPurple,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 10 {display:{Name:"Control Point A"}}
@e[BCPA,BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 3 {display:{Name:"Control Point A"}}
@e[BCPA,BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 6 {display:{Name:"Control Point A"}}
@e[BCPA,BiolabBCP=0,team=BiolabCPPurple,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 2 {display:{Name:"Control Point A"}}

@e[BCPB,BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 11 {display:{Name:"Control Point B"}}
@e[BCPB,BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 14 {display:{Name:"Control Point B"}}
@e[BCPB,BiolabBCP=0,team=BiolabCPPurple,BiolabCPIS=2] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 10 {display:{Name:"Control Point B"}}
@e[BCPB,BiolabBCP=0,team=BiolabCPBlue,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 3 {display:{Name:"Control Point B"}}
@e[BCPB,BiolabBCP=0,team=BiolabCPRed,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 6 {display:{Name:"Control Point B"}}
@e[BCPB,BiolabBCP=0,team=BiolabCPPurple,BiolabCPIS=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 2 {display:{Name:"Control Point B"}}

@e[BCPA] @e[BiolabControlPointVisual,team=BiolabCPNeutral,r=0,c=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.4 minecraft:stained_glass 1 0 {display:{Name:"Control Point A"}}
@e[BCPB] @e[BiolabControlPointVisual,team=BiolabCPNeutral,r=0,c=1] replaceitem entity @a[BiolabPlayer] slot.hotbar.5 minecraft:stained_glass 1 0 {display:{Name:"Control Point B"}}

// Incrementing team scores
@e[BiolabCPIS=2,team=BiolabCPBlue] Blue_Team BiolabTS + 10
@e[BiolabCPIS=1,team=BiolabCPBlue] Blue_Team BiolabTS + 5

@e[BiolabCPIS=2,team=BiolabCPRed] Red_Team BiolabTS + 10
@e[BiolabCPIS=1,team=BiolabCPRed] Red_Team BiolabTS + 5

// Winner check
scoreboard players set ScoreCalc BiolabTemp 0
scoreboard players test Blue_Team BiolabTS scoreTarget
CONDITION
	ScoreCalc BiolabTemp + 1
scoreboard players test Red_Team BiolabTS scoreTarget
CONDITION
	ScoreCalc BiolabTemp + 2

scoreboard players test ScoreCalc BiolabTemp 1 1
CONDITION
	title @a[BiolabPlayer] title '[{"text":"","color":"green"},{"text":"Blue","color":"blue"}," team has won!"]'

scoreboard players test ScoreCalc BiolabTemp 2 2
CONDITION
	title @a[BiolabPlayer] title '[{"text":"","color":"green"},{"text":"Red","color":"red"}," team has won!"]'

scoreboard players test ScoreCalc BiolabTemp 3 3
CONDITION
	title @a[BiolabPlayer] title '[{"text":"","color":"green"},{"text":"Blue","color":"blue"}," and ",{"text":"Red","color":"red"}," have tied!"]'

scoreboard players test ScoreCalc BiolabTemp 1
CONSTART
	deactivate(controlPointUpdateClock)
	activate(gameEnd)
CONEND

FUNC disableControlPoints
	deactivate(controlPointUpdateClock)
	scoreboard objectives remove BiolabCPI
	scoreboard objectives remove BiolabCPIS
	scoreboard objectives remove BiolabBCP
	scoreboard objectives remove BiolabCPPC
	scoreboard objectives remove BiolabTS
	kill @e[BiolabControlPoint]
	kill @e[BiolabControlPointVisual]
END
